using WGLMakie
using PointProcesses
using DrWatson
using Distributions

function continuous2discrete(h::History, nframes::Int)
    frame = 1
    tmin = min_time(h)
    tmax = max_time(h)
    discrete_grid = range(tmin, tmax, nframes + 1)[2:end]
    discrete_t = discrete_grid[1]
    times = event_times(h)
    marks = event_marks(h)
    Nneur = max_mark(h)

    output = zeros(Bool, Nneur, nframes)
    for (i, t) in enumerate(times)
        while t > discrete_t
            frame += 1
            discrete_t = discrete_grid[frame]
        end

        output[marks[i], frame] = 1
    end

    return output
end

function fadedots(; fps::Int=60, fade_rate=0.01)
    while any(alphas[] .!= 0)
        alphas[] .= clamp.((alphas[] .- fade_rate), 0.0, 1.0)
        sleep(1 / fps)
    end
end

function spikesanimation(spikes::Matrix; fps::Int=60, fade_rate=0.01)
    # Reset scatter plot
    stopflag[] = true
    fadedots(fps=fps, fade_rate=fade_rate) # fade existing dots
    sleep(0.1)

    nframes = size(spikes)[2]
    # Start new animation
    stopflag[] = false
    @async begin
        for frame in 1:nframes
            stopflag[] == true && break

            # Fade existing dots
            alphas[] .= clamp.((alphas[] .- fade_rate), 0.0, 1.0)

            # Add new dots
            alphas[][findall(spikes[:, frame] .== true)] .= 1.0

            notify(alphas)
            sleep(1 / fps)
        end
        # Fade remaining dots
        for _ in 1:ceil(Int, 1 / fade_rate)
            alphas[] .= clamp.((alphas[] .- fade_rate), 0.0, 1f0)
            notify(alphas)
            sleep(1 / fps)
        end
    end
end

function spikesanimation(; fps::Int=60, fade_rate=0.01)
    stopflag[] = true
    fadedots(fps=fps, fade_rate=fade_rate) # fade existing dots
    sleep(0.1)

    # Start new animation
    stopflag[] = false
    @async begin
        while !stopflag[]
            # Fade existing dots
            alphas[] .= clamp.(alphas[] .- fade_rate, 0.0, 1.0)

            if rand() < 0.05
                alphas[][rand(DiscreteUniform(1, Nneur))] = 1.0
            end
            notify(alphas)
            sleep(1 / fps)
        end
        # Fade remaining dots
        for _ in 1:ceil(Int, 1 / fade_rate)
            alphas[] .= clamp.((alphas[] .- fade_rate), 0.0, 1f0)
            notify(alphas)
            sleep(1 / fps)
        end
    end
end

begin # Load simulated data 
    # Load positions of neurons corresponding to a SHP network with 900 neurons
    positions = load("data/SHP-animation.jld2", "positions")
    Nneur = length(positions)

    # Load spikes
    ## Initialize a vector of spike data
    spikes = Matrix{Bool}[]
    ## Load spike history made from a ADHP network with 900 neurons
    ### generated by animation-ADHP.jl
    h_adhp = load("data/ADHP-animation.jld2", "history")
    push!(spikes, continuous2discrete(h_adhp, 2000))
    ## Load spike history made from a SHP network with 900 neurons
    ### generated by animation-SHP.jl
    h_shp = load("data/SHP-animation.jld2", "history")
    push!(spikes, continuous2discrete(h_shp, 2000))
    ## Load spike history made from a 2CHP network with 900 neurons
    ### generated by animation-2CHP.jl
    h_2chp = load("data/2CHP-animation.jld2", "history")
    push!(spikes, continuous2discrete(h_2chp, 2000))
    ## Load discrete spikes made from a MCRE network with 900 neurons
    ### generated by animation-MCRE.jl
    push!(spikes, load("data/MCRE-animation.jld2", "spikes"))
end

begin # Animation setup
    # Figure setup
    fig = Figure(size=(400, 200), backgroundcolor=:black)
    buttongrid = fig[1, 1] = GridLayout(tellheight=false)
    startbutton = buttongrid[1, 1] = Makie.Button(fig, label="Start")

    # Observables
    alphas = Observable(zeros(Nneur))
    stopflag = Observable(false)

    on(startbutton.clicks) do _
        # Figure update
        ## Remove start button
        empty!(startbutton.blockscene)
        delete!(startbutton)
        ## Initialize scatter plot
        ax = Axis(fig[1, 2], backgroundcolor=:black, aspect=AxisAspect(1), topspinecolor=:white, leftspinecolor=:white, bottomspinecolor=:white, rightspinecolor=:white)
        scatter!(ax, positions, color=@lift(RGBAf.(1, 1, 1, $alphas)), markersize=8)
        ## Initialize buttons
        buttons = buttongrid[1:4, 1] = [Makie.Button(fig, label="Pattern " * string(i)) for i in 1:4]

        fps = 60
        fade_rate = 0.01
        spikesanimation(; fps=fps, fade_rate=fade_rate)

        for buttonid in 1:4
            on(buttons[buttonid].clicks) do _
                spikesanimation(spikes[buttonid]; fps=fps, fade_rate=fade_rate)
            end
        end
    end
end

display(fig)
